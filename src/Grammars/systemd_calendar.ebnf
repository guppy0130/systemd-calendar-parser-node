/* substring matching/first match (instead of greedy), so order is important */
FullCalendarSpec ::= ((SpecialExpressions) | (WeekDaySpec? WS? DateSpec? WS? TimeSpec?)) (WS TimezoneSpec)?

WeekDaySpec ::= (WeekDaySpecEntry ",")* WeekDaySpecEntry

WeekDaySpecEntry ::= RangedWeekDaySpec | DayOfTheWeek
RangedWeekDaySpec ::= DayOfTheWeek RANGE_OP DayOfTheWeek

/* case insensitivity */
DayOfTheWeek ::= SUNDAY | MONDAY | TUESDAY | WEDNESDAY | THURSDAY | FRIDAY | SATURDAY
SUNDAY ::= [Ss][Uu][Nn]([Dd][Aa][Yy])?
MONDAY ::= [Mm][Oo][Nn]([Dd][Aa][Yy])?
TUESDAY ::= [Tt][Uu][Ee]([Ss][Dd][Aa][Yy])?
WEDNESDAY ::= [Ww][Ee][Dd]([Nn][Ee][Ss][Dd][Aa][Yy])?
THURSDAY ::= [Tt][Hh][Uu]([Rr][Ss][Dd][Aa][Yy])?
FRIDAY ::= [Ff][Rr][Ii]([Dd][Aa][Yy])?
SATURDAY ::= [Ss][Aa][Tt]([Uu][Rr][Dd][Aa][Yy])?

DateSpec ::= (Year "-" Month ("-" | "~") Day) | (Month ("-" | "~") Day)

Year ::= (YearEntry ",")* YearEntry
YearEntry ::= RangedYearUnit | RepeatedYearUnit | YearUnit
RangedYearUnit ::= YearUnitNoWildcard RANGE_OP (RepeatedYearUnit | YearUnitNoWildcard)
RepeatedYearUnit ::= YearUnitNoWildcard REPEAT_OP YearUnitNoWildcard
YearUnit ::= YearUnitNoWildcard | WILDCARD
YearUnitNoWildcard ::= DIGIT DIGIT? DIGIT? DIGIT?

Month ::= TimeUnitSpec
Day ::= TimeUnitSpec

TimeSpec ::= Hour ":" Minute (":" Second)?

Hour ::= TimeUnitSpec
Minute ::= TimeUnitSpec
Second ::= SecondUnitSpecEntry

/* 1,2 */
TimeUnitSpec ::= (TimeUnitSpecEntry ",")* TimeUnitSpecEntry
/* 1..3 OR 1/3 OR 1 */
TimeUnitSpecEntry ::= RangedTimeUnit | RepeatedTimeUnit | TimeUnit
/* 01..03 OR 01..03/2 */
RangedTimeUnit ::= TimeUnitNoWildcard RANGE_OP (RepeatedTimeUnit | TimeUnitNoWildcard)
/* 01/3 */
RepeatedTimeUnit ::= TimeUnitNoWildcard REPEAT_OP TimeUnitNoWildcard

TimeUnit ::= TimeUnitNoWildcard | WILDCARD
TimeUnitNoWildcard ::= DIGIT DIGIT?

SecondUnitSpec ::= (SecondUnitSpecEntry ",")* SecondUnitSpecEntry
SecondUnitSpecEntry ::= RangedSecondUnit | RepeatedSecondUnit | SecondUnit
/* 03.333..06.333/0.111 */
RangedSecondUnit ::= SecondUnitNoWildcard RANGE_OP (RepeatedSecondUnit | SecondUnitNoWildcard)
/* 03.333/0.333 */
RepeatedSecondUnit ::= SecondUnitNoWildcard REPEAT_OP SecondUnitNoWildcard

SecondUnit ::= SecondUnitNoWildcard | WILDCARD
/* 03 OR 03.333 */
SecondUnitNoWildcard ::= (DIGIT DIGIT? ("." DIGIT+)?)

TimezoneSpec ::= (ALPHA+ "/")* ALPHA+

SpecialExpressions ::= "minutely" | "hourly" | "daily" | "weekly" | "monthly" | "quarterly" | "semiannually" | "yearly" | "annually"

ALPHA ::= [a-zA-Z]
DIGIT ::= [0-9]
RANGE_OP ::= ".."
REPEAT_OP ::= "/"
WILDCARD ::= "*"
WS ::= " "
